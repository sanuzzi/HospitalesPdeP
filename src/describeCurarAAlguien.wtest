import hospital.*
import persona.*
import conocimientoDeMedico.*

describe "Curar a alguien" {

	const alguienInicialmenteEnfermo
	const unDoctorCualquiera
	const hospitalConDoctores

	fixture {
		alguienInicialmenteEnfermo = new Persona()
		alguienInicialmenteEnfermo.enfermate()
		unDoctorCualquiera = new Persona(
			conocimientoDeMedicina = new ConocimientoDeMedico()
		)
		hospitalConDoctores = new Hospital(
			curadores = [ unDoctorCualquiera, unDoctorCualquiera ]
		)
	}

	test "La persona enferma, no tiene buena salud" {
		assert.notThat(alguienInicialmenteEnfermo.tenesBuenaSalud())
	}

	test "si un doctor cura a un enfermo, pasa a tener buena salud" {
		unDoctorCualquiera.cura(alguienInicialmenteEnfermo)
		assert.that(alguienInicialmenteEnfermo.tenesBuenaSalud())
	}

	test "si un doctor cura a un enfermo, la calidad del doctor aumenta" {
		const calidadInicial = unDoctorCualquiera.calidad()
		unDoctorCualquiera.cura(alguienInicialmenteEnfermo)
		const calidadFinal = unDoctorCualquiera.calidad()
		assert.that(calidadFinal > calidadInicial)
	}

	test "Si hay un doctor de 10 puntos y otro de 1000, el peor es el de 10" {
		const doctorReCapo = new Persona(
			conocimientoDeMedicina = new ConocimientoDeMedico(calidad = 10000)
		)
		const doctorRecienRecibido = new Persona(
			conocimientoDeMedicina = new ConocimientoDeMedico(calidad = 10)
		)
		const unHospital = new Hospital(
			curadores = [ doctorReCapo, doctorRecienRecibido ]
		)
		assert.equals(unHospital.peorCurador(), doctorRecienRecibido)
	}

	test "si el hospital cura a un enfermo, pasa a tener buena salud" {
		hospitalConDoctores.cura(alguienInicialmenteEnfermo)
		assert.that(alguienInicialmenteEnfermo.tenesBuenaSalud())
	}

	test "si el hospital cura a un enfermo, se agrega entre los pacientes atendidos" {
		hospitalConDoctores.cura(alguienInicialmenteEnfermo)
		assert.that(hospitalConDoctores.atendiste(alguienInicialmenteEnfermo))
	}

	test "si el hospital solo tiene a otro hospital dentro, la persona se cura igual" {
		const hospitalGrandote = new Hospital(curadores = [ new Hospital(
				curadores = [unDoctorCualquiera]
			) ])
		hospitalGrandote.cura(alguienInicialmenteEnfermo)
		assert.equals(alguienInicialmenteEnfermo.tenesBuenaSalud())
	}

	test "Una persona NO médica tiene calidad 0" {
		const unaPersonaComun = new Persona()
		assert.equals(unaPersonaComun.calidad(), 0)
	}

	test "Una persona NO médica al curar a alguien sano, hace que deje de tener buena salud" {
		const unaPersonaComun = new Persona()
		const unaPersonaSana = new Persona()
		unaPersonaSana.curate() // Para garantizar que esté sana
		unaPersonaComun.cura(unaPersonaSana)
		assert.notThat(unaPersonaSana.tenesBuenaSalud())
	}

	test "Si una persona estudia medicina, logra hacer que alguien tenga buena salud" {
		const unFuturoDoctor = new Persona()
		unFuturoDoctor.estudiaMedicina()
		unFuturoDoctor.cura(alguienInicialmenteEnfermo)
		assert.that(alguienInicialmenteEnfermo.tenesBuenaSalud())
	}

}

