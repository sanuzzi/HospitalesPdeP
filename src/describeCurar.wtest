import hospital.*
import persona.*
import doctor.*

describe "tests sobre curar a alguien" {
	const enferma
	
	fixture {
		enferma = new Persona()
		enferma.enfermate()
	}

	test "si sandra se cura, tiene 100 puntos de salud" {
		enferma.curate()
		
		assert.that(enferma.tenesBuenaSalud())
	}
	
	test "cuando un doctor cura a alguien, ésta queda con buena salud"{
		new Doctor().cura(enferma)
		
		assert.that(enferma.tenesBuenaSalud())
	}
	
	test "si un doctor cura a alguien, aumenta su calidad"{
		const lucas = new Doctor()
		
		const calidadInicial = lucas.calidadDeAtencion()
		
		lucas.cura(enferma)
		
		const calidadFinal = lucas.calidadDeAtencion()
		
		assert.that(calidadFinal > calidadInicial)
	}
	
	test "si hay un doctor de calidad 10 y otros de 100, el mínimo es el de 10"{
		const elPeorDeTodos = new Doctor(calidadDeAtencion = 10)
		const lucas = new Doctor(calidadDeAtencion = 100)
		const pepe = new Doctor(calidadDeAtencion = 100)
		
		hospital.doctores([pepe, elPeorDeTodos, lucas])
		
		assert.equals(hospital.peorDoctor(), elPeorDeTodos)
	}
	
	test "si el hospital cura a alguien, éste queda con buena salud"{
		hospital.doctores([new Doctor(), new Doctor(), new Doctor()])
		
		hospital.cura(enferma) 
		
		assert.that(enferma.tenesBuenaSalud())
	}
	
	test "si el hospital cura a alguien, éste queda entre los pacientes atendidos"{
		hospital.doctores([new Doctor(), new Doctor(), new Doctor()])
		
		hospital.cura(enferma) 
		
		assert.that(hospital.atendiste(enferma))
	}
}


